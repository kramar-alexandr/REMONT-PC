SetLangMode(LangRussian,"RUS",0);


global procedure ErpJsRn(record RcVc RepSpec)
begin
	record SVOVc SVOr;
	row SVOVc SVOrw;
	boolean TrHs,testf;
	vector string 100 vOrders;
	record CUVc CUr;
	vector boolean vPartners;
	vector string 255 vClient,vPhone,vDevice,vSpec,vComment1,vDate,StatusText;
	array string 255 aPartners,aOrders;
	integer i,j,qty;
	
	
	startreportnoheaderjob("Переданные заказы партерам");
	
	startformat(15);
		outstring(0,0,"Период:",false);
		outstring(60,0,RepSpec.sStartDate,false);
		outstring(120,0,RepSpec.sEndDate,false);
	endformat;
	black_divider(0,1);
	
	startformat(15);
		outstring(0,0,"Код парт.",false);
		outstring(50,0,"Назв. парт.",false);
		outstring(150,0,"Тел. партн.",false);
	endformat;
	
	startformat(15);
		outstring(0,0,"Дата",false);
		outstring(50,0,"Заказ",false);
		outstring(90,0,"Клиент",false);
		outstring(180,0,"Телефон",false);
		outstring(220,0,"Артикул устр.",false);
		outstring(250,0,"Устройство",false);
		outstring(350,0,"Жалобы",false);
		outstring(400,0,"Статус",false);
	endformat;
	
	SVOr.TransDate = RepSpec.sStartDate;
	TrHs = true;
	while(loopkey("TransDate",SVOr,1,TrHs))begin
		testf = true;
		if(SVOr.TransDate>RepSpec.sEndDate)then begin TrHs = false; testf = false; end;
		if(blank(SVOr.SendToPartner))then begin testf = false; end;
		if(SVOr.SendedToPartner==0)then begin testf = false; end;
		
		if(testf)then begin
			vOrders[SVOr.SerNr] = SVOr.SendToPartner;
			vDate[SVOr.SerNr] = SVOr.TransDate;
			vPartners[SVOr.SendToPartner] = true;
			vClient[SVOr.SerNr] = SVOr.Addr0;
			vPhone[SVOr.SerNr] = SVOr.Kontinfo1;
			if(matrowcnt(SVOr)>0)then begin
				matrowget(SVOr,0,SVOrw);
			end;
			vDevice[SVOr.SerNr] = SVOrw.ArtCode;
			vSpec[SVOr.SerNr] = SVOrw.Spec;
			vComment1[SVOr.SerNr] = SVOr.Comment1;
			switch(SVOr.OrderStatus)begin
				case 0: StatusText[SVOr.SerNr] = USetStr(31044);
				case 1: StatusText[SVOr.SerNr] = USetStr(31045);
				case 3: StatusText[SVOr.SerNr] = USetStr(31050);
				case 2: StatusText[SVOr.SerNr] = USetStr(31046);
				case 4: StatusText[SVOr.SerNr] = USetStr(31061);
				case 5: StatusText[SVOr.SerNr] = USetStr(31085);
				case 6: StatusText[SVOr.SerNr] = USetStr(31095);
				case 7: StatusText[SVOr.SerNr] = USetStr(31096);
				case 8: StatusText[SVOr.SerNr] = USetStr(31097);
				case 9: StatusText[SVOr.SerNr] = USetStr(31501);
				case 10: StatusText[SVOr.SerNr] = USetStr(31506);
				case 11: StatusText[SVOr.SerNr] = USetStr(31509);
				case 12: StatusText[SVOr.SerNr] = USetStr(31510);
			end;
		end;			
	end;
	
	getVectorTags(vPartners,aPartners);
	getVectorTags(vOrders,aOrders);
	for(i=0;i<aPartners.length;i=i+1)begin
		qty = 0;
		CUr.Code = aPartners[i];
		if(readfirstmain(CUr,1,true))then begin
			startformat(15);
				outstring(0,0,CUr.Code,false);
				outstring(50,0,CUr.Name,false);
				outstring(150,0,CUr.Phone,false);
			endformat;
			
			
			for(j=0;j<aOrders.length;j=j+1)begin
				if(vOrders[aOrders[j]]==aPartners[i])then begin
					qty = qty + 1;
					startformat(15);
						outstring(0,0,vDate[aOrders[j]],false);
						outstring(50,"DblSVOVc",aOrders[j],false);
						outstring(90,0,vClient[aOrders[j]],false);
						outstring(180,0,vPhone[aOrders[j]],false);
						outstring(220,0,vDevice[aOrders[j]],false);
						outstring(250,0,vSpec[aOrders[j]],false);
						outstring(350,0,vComment1[aOrders[j]],false);
						outstring(400,0,StatusText[aOrders[j]],false);
					endformat;
				end;
			end;
			startformat(15);
				outstring(0,0,"Всего ремонтов",false);
				outstring(70,0,qty,false);
			endformat;
			
		end;
		black_divider(0,1);
	end;
	
	
	endjob;
return;
end;